KNE-00001 : Kona A New Programming Language
===========================================

Summary
-------

This is the first Kona proposal. It introduces a tiny programming language,
Kona, with minimal features. In this proposal, Kona is not yet a usable
language, it is very rudimentary and lacks of many basic features. This is
because we don't want Kona to be too complex at the start. We believe it is
better to add new features step by step. Even the most basic things like tuple,
record, ADT, need to be well considered. They are split up into later proposals
instead of here.

Despite the temporary absence of some basic abstraction facilities, Kona is
already taking shape. You can see it is a purely functional ML family language
with a traditional type system, and heavily influenced by Standard ML [^1] [^2].

```
let sayHello = fn name => "Hello, " ^ name ^ "!" in
print (sayHello "world") -- Prints "Hello, world!"
```

This proposal will detail the syntax (including lexical structure) of the Kona
prototype in a variant of W3C EBNF [^3], but without the semantic part. We want
to leave the semantic unspecified until the language is a bit more mature. But
one thing is clear: Kona's type checker is based on the Hindley-Milner type
system.

The grammar described here is intended to help you understand the language in
more detail, rather than to allow you to directly implement a parser or
compiler.

Lexical Structure
-----------------

The lexical structure of Kona describes what sequence of characters form valid
tokens of the language. These valid tokens form the lowest-level building blocks
of the language and are used to describe the rest of the language in subsequent
chapters. A token consists of an identifier, keyword, punctuation, or literal.
The behavior of the lexical analyzer follows the longest match (aka. maximal
munch) principle.

### WhiteSpace and Comments

Whitespace is used to separate tokens, it will be ignored by the lexer. The
following characters are considered whitespace: space (`U+0020`), line feed
(`U+000A`), carriage return (`U+000D`), horizontal tab (`U+0009`), vertical tab
(`U+000B`), form feed (`U+000C`) and null (`U+0000`).

```
whitespace        ::= whitespace-item whitespace?
whitespace-item   ::= end-of-line
                    | inline-space
                    | single-line-comment
                    | multi-line-comment
                    | U+0000 | U+000B | U+000C

end-of-line       ::= U+000A | U+000D | U+000D U+000A

inline-spaces     ::= inline-space inline-spaces?
inline-space      ::= U+0009 | U+0020
```

Comments are treated as whitespace by the compiler. Single line comments begin
with `--` and continue until a line feed (`U+000A`) or carriage return
(`U+000D`). Multiline comments begin with `/-` and end with `-/`. Nesting
multiline comments is allowed, but the comment markers must be balanced.

```
single-line-comment           ::= '--' comment-text end-of-line
multi-line-comment            ::= '/-' multi-line-comment-text '-/'

single-line-comment-text      ::= comment-text-item comment-text?
single-line-comment-text-item ::= Any Unicode scalar value except U+000A or U+000D

multi-line-comment-text       ::= multi-line-comment-text-item multi-line-comment-text?
multi-line-comment-text-item  ::= multi-line-comment
                                | Any Unicode scalar value except '/-' or '-/'
```

### Keywords and Punctuation

The following keywords are reserved and canâ€™t be used as identifiers: `else`,
`fn`, `if`, `infix`, `in`, `let`, and `then`.

```
keyword ::= 'else' | 'fn' | 'if' | 'infix' | 'in' | 'let' | 'then'
```

The following tokens are reserved as punctuation: `(`, `)`, `;`, `=`, and `=>`.

```
punctuation ::= ';' | '(' | ')' | '=' | '=>'
```

### Identifiers

Identifiers begin with an uppercase or lowercase letter A through Z, or an
underscore (`_`). After the first character, digits are also allowed.

```
identifier            ::= identifier-head identifier-characters?
identifier-head       ::= Upper- or lowercase letter A through Z
                        | '_'
identifier-character  ::= identifier-head
                        | Digit 0 through 9
identifier-characters ::= identifier-char identifier-characters?
```

### Literals

A literal is the source code representation of a value of a type, such as a
number or string. Kona supports literals for integers, floating-point numbers,
booleans, and strings.

```
literal ::= numeric-literal
          | boolean-literal
          | string-literal
```

Numeric literals represent integers and floating-point number values.

```
numberic-literal ::= '-'? integer-literal
                   | '-'? floating-point-literal

integer-literal ::= decimal-digits
floating-point-literal ::= decimal-digits '.' decimal-digits
decimal-digit ::= Digit 0 through 9
decimal-digits ::= decimal-digit decimal-digits?
```

A string literal is a sequence of characters surrounded by quotation marks.

Special characters can be included in string literals using the following escape
sequences:

- Null character (`\0`);
- Backslash (`\\`);
- Horizontal tab (`\t`);
- Line feed (`\n`);
- Carriage return (`\r`);
- Double quotation mark (`\"`);

```
string-literal ::= '"' string-literal-characters '"'

quoted-text ::= quoted-text-item quoted-text?
quoted-text-item ::= escaped-character
                   | Any Unicode scalar value except ", \, U+000A, or U+000D

escaped-character ::= escape-sequence '0'
                    | escape-sequence '\\'
                    | escape-sequence 't'
                    | escape-sequence 'n'
                    | escape-sequence 'r'
                    | escape-sequence '"'
escape-sequence ::= '\\'
```

Boolean literals are `true` or `false`, they are not keywords, but still cannot
used as identifiers.

```
boolean-literal ::= 'true' | 'false'
```

Syntax
------

```
top-level ::= expression

atomic-expression ::= literal-expression
                    | identifier
                    | group-expression

application-expression ::= atomic-expression
                         | atomic-expression atomic-expression

infix-expression ::= application-expression
                   | application-expression operator application-expression

expression ::= infix-expression
             | lambda-expression
             | if-expression
             | group-expression

lambda-expression ::= 'fn' identifier '=>' expression

if-expression ::= 'if' expression 'then' expression 'else' expression

group-expression       ::= '(' group-expression-list? ')'
group-expression-list  ::= group-expression-items expression?
                         | expression
group-expression-item  ::= ';'
                         | expression ';'
group-expression-items ::= group-expression-item group-expression-items?
```

[^1]: Milner, Robin, Mads Tofte and Robert Harper. "Definition of standard ML."
      (1990). https://smlfamily.github.io/sml90-defn.pdf
[^2]: Milner, Robin, Robert Harper, David B. MacQueen and Mads Tofte. "The
      Definition of Standard ML (Revised)."  (1997).
      https://smlfamily.github.io/sml97-defn.pdf
[^3]: "Extensible Markup Language (XML) 1.0 (Fifth Edition).", Section 6
      Notation. https://www.w3.org/TR/REC-xml/#sec-notation
