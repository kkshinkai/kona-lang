KNE-00001 : Kona A New Programming Language
===========================================

Summary
-------

This is the first Kona proposal. It introduces a tiny programming language,
Kona, with minimal features. In this proposal, Kona is not yet a usable
language, it is very rudimentary and lacks of many basic features. This is
because we don't want Kona to be too complex at the start. We believe it is
better to add new features step by step. Even the most basic things like tuple,
record, ADT, need to be well considered. They are split up into later proposals
instead of here.

Despite the temporary absence of some basic abstraction facilities, Kona is
already taking shape. You can see it is a purely functional ML family language
with a traditional type system, and heavily influenced by Standard ML [^1] [^2].

    let sayHello = fn name => "Hello, " ^ name ^ "!" in
    print (sayHello "world") -- Prints "Hello, world!"

This proposal will detail the syntax (including lexical structure) of the Kona
prototype in a variant of W3C EBNF [^3], but without the semantic part. We want
to leave the semantic unspecified until the language is a bit more mature. But
one thing is clear: Kona's type checker is based on the Hindley-Milner type
system.

Lexical Structure
-----------------

The lexical structure of Kona describes what sequence of characters form valid
tokens of the language. These valid tokens form the lowest-level building blocks
of the language and are used to describe the rest of the language in subsequent
chapters. A token consists of an identifier, keyword, punctuation, or literal.
The behavior of the lexical analyzer follows the longest match (aka. maximal
munch) principle.

```ebnf
Whitespace               ::= WhitespaceItem Whitespace?
WhitespaceItem           ::= EndOfLine
                           | InlineSpace
                           | SingleLineComment
                           | MultiLineComment
                           | U+0000 | U+000B | U+000C

EndOfLine                ::= U+000A | U+000D | U+000D U+000A

InlineSpaces             ::= InlineSpace InlineSpaces?
InlineSpace              ::= U+0009 | U+0020

SingleLineComment        ::= '--' CommentText EndOfLine
MultiLineComment         ::= '/-' MultiLineCommentText '-/'

CommentText              ::= CommentTextItem CommentText?
CommentTextItem          ::= Any Unicode scalar value except U+000A or U+000D

MultiLineCommentText     ::= MultiLineCommentTextItem MultiLineCommentText?
MultiLineCommentTextItem ::= MultiLineComment
                           | Any Unicode scalar value except '/-' or '-/'


Literal                  ::= NumericLiteral
                           | StringLiteral
                           | BooleanLiteral
StringLiteral            ::= '"' QuotedText '"'
QuotedText               ::= QuotedTextItem QuotedText?
QuotedTextItem           ::= EscapedCharacter
                           | Any Unicode scalar value except '"', \, U+000A, or U+000D
EscapedCharacter         ::= '\\0' | '\\\\' | '\\t' | '\\n' | '\\r'
NumericLiteral           ::= '~'? IntegerLiteral
                           | '~'? FloatingPointLiteral
IntegerLiteral           ::= DigitSequence
FloatingPointLiteral     ::= DigitSequence '.' DigitSequence
DigitSequence            ::= [0-9]+
BooleanLiteral           ::= 'true' | 'false'

Keyword                  ::= 'else' | 'end' | 'fn' | 'if' | 'in' | 'let'
                           | 'infix' | 'then' | '=' | '=>'

Identifier               ::= AlphanumericIdentifier | SymbolicIdentifier - Keyword

AlphanumericIdentifier   ::= [a-zA-Z_][a-zA-Z0-9_]*

SymbolicIdentifier       ::= SymbolicChar+
SymbolicChar             ::= '!' | '%' | '&' | '$' | '+' | '-' | ':' | '<'
                           | '=' | '>' | '?' | '/' | '~' | '^' | '|' | '*'

Punctuation              ::= ";" | "(" | ")" | ":"
```

Syntax
------

TODO

[^1]: Milner, Robin, Mads Tofte and Robert Harper. "Definition of standard ML."
      (1990). https://smlfamily.github.io/sml90-defn.pdf
[^2]: Milner, Robin, Robert Harper, David B. MacQueen and Mads Tofte. "The
      Definition of Standard ML (Revised)."  (1997).
      https://smlfamily.github.io/sml97-defn.pdf
[^3]: "Extensible Markup Language (XML) 1.0 (Fifth Edition).", Section 6
      Notation. https://www.w3.org/TR/REC-xml/#sec-notation
